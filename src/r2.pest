Program = { SOI ~ (Definition | SExpr)* ~ EOI }

REPLInput = { SOI ~ (Definition | SExpr)? ~ EOI }

SExpr = {
    Literal
    | Identifier
    | SList
}

// This return Unit value
Definition = { "(" ~ DefSyntax ~ ")" }

DefSyntax = _{ "define" ~ Identifier ~ SExpr }

SList = _{ "(" ~ CalcSyntax ~ ")" }

CalcSyntax = _{
    LambdaDef
    | Bind
    | Block
    | Application
}

// the last item must be a expression
BlockSyntax = _{ Definition* ~ (SExpr+ ~ Definition+)* ~ SExpr+ }

LambdaDef = { LAMBDA_SIGN ~ "(" ~ Identifier ~ ")" ~ BlockSyntax }

LAMBDA_SIGN = _{ "lambda" | "Î»" }

Bind = { "let" ~ "(" ~ BindPair ~ ")" ~ BlockSyntax }

BindPair = _{
    "(" ~ Identifier ~ SExpr ~ ")"
    | "[" ~ Identifier ~ SExpr ~ "]"
}

Block = { "begin" ~ BlockSyntax }

Application = { SExpr+ }

Literal = _{
    Number
}

Number = _{
    Integer
}

Integer = @{
    ASCII_DIGIT+
}

Identifier = @{
    !Literal ~ !Keyword ~ (ASCII_ALPHANUMERIC | SCHEME_EXT)+
}

Keyword = { "lambda" | "let" | "define" | "begin" }

SCHEME_EXT = { 
    "!" | "$" | "%" | "&" |
    "*" | "+" | "-" | "." |
    "/" | ":" | "<" | "=" |
    ">" | "?" | "@" | "^" |
    "_" | "~"
}

WHITESPACE = _{ WHITE_SPACE }
COMMENT    = _{ ";" ~ (!NEWLINE ~ ANY)* }
