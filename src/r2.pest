Program = { SOI ~ SExpr* ~ EOI }

SingleSExpr = { SOI ~ SExpr ~ EOI }

SExpr = {
    Literal
    | Identifier
    | SList
}

SList = _{ "(" ~ SyntaxStruct ~ ")" }

SyntaxStruct = _{
    LambdaDef
    | Bind
    | Definition
    | Block
    | Application
}

LambdaDef = { "lambda" ~ "(" ~ Identifier ~ ")" ~ SExpr+ }

Bind = { "let" ~ "(" ~ BindPair ~ ")" ~ SExpr+ }

BindPair = _{
    "(" ~ Identifier ~ SExpr ~ ")"
    | "[" ~ Identifier ~ SExpr ~ "]"
}

// This return Unit value
Definition = { "define" ~ Identifier ~ SExpr }

Block = { "begin" ~ SExpr+ }

Application = { SExpr+ }

Literal = _{
    Number
}

Number = _{
    Integer
}

Integer = @{
    ASCII_DIGIT+
}

Identifier = @{
    !Literal ~ !Keyword ~ (ASCII_ALPHANUMERIC | SCHEME_EXT)+
}

Keyword = { "lambda" | "let" }

SCHEME_EXT = { 
    "!" | "$" | "%" | "&" |
    "*" | "+" | "-" | "." |
    "/" | ":" | "<" | "=" |
    ">" | "?" | "@" | "^" |
    "_" | "~"
}

WHITESPACE = _{ WHITE_SPACE }
COMMENT    = _{ ";" ~ (!NEWLINE ~ ANY)* }
